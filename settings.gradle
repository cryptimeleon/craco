plugins {
    id 'org.ajoberstar.grgit' version '4.0.2' apply false
}
import org.ajoberstar.grgit.Grgit

rootProject.name = 'craco'


def mathPath = file("../upb.crypto.math").getPath()
def grgitMath
if (file(mathPath).exists()) {
    println("upb.crypto.math: Local repository exists.")
    grgitMath = Grgit.open(dir: mathPath)
} else {
    println("upb.crypto.math: Local repository does not exist. Cloning to " + mathPath + ".")
    grgitMath = Grgit.clone(dir: mathPath, uri: "https://github.com/upbcuk/upb.crypto.math.git")
}
def grgitThis = Grgit.open()
def branchThis = grgitThis.branch.current()
if (file(mathPath + "/.git/refs/heads/" + branchThis.getName()).exists()) {
    if (grgitMath.branch.current().getName() == branchThis.getName()) {
        println("upb.crypto.math: Branch " + branchThis.getName() + " exists and is checked out already.")
    } else {
        throw new GradleException("upb.crypto.math: Branch " + branchThis.getName() + " exists but is not checked out. " +
                                  "Please check it out yourself before building.")
    }
} else {
    println("upb.crypto.math: Branch " + branchThis.getName() + " does not exist locally.")
    if (grgitMath.branch.list{mode = "REMOTE"}.collect{it.getName()}.contains("origin/" + branchThis.getName())) {
        println("upb.crypto.math: Branch " + branchThis.getName() + " exists remotely. Creating a local branch.")
        grgitMath.checkout {
            branch = branchThis.getName()
            startPoint = "origin/" + branchThis.getName()
            createBranch = true
        }
    } else {
        println("upb.crypto.math: Branch " + branchThis.getName() + " does not exist remotely. Using master.")
    }
}

println("upb.crypto.math: Enabling composite build.")
includeBuild (mathPath)